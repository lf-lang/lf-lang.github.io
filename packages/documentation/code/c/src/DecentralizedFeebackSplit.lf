target C {
    timeout: 5 sec,
    coordination: decentralized
}
reactor Count {
    output out:int;
    state c:int(0);
    timer t(0, 500 msec);
    reaction(t) -> out {=
        lf_set(out, self->c++);
    =}
}

reactor Print {
    input in:int;
    reaction(in) {=
        lf_print("***** Print Received: %d at time %lld",
            in->value, lf_time_logical_elapsed()
        );
    =} STP(10 msec) {=
        lf_print_warning("Print: Safe to process violation!");
        lf_print("Intended time: %lld", in->intended_tag.time - lf_time_start());
    =}
}

reactor PrintCount {
    input in:int;
    output out:int;
    timer t(0, 999 msec);
    state c:int(0);
    reaction(in) {=
        lf_print("***** PrintCount Received: %d", in->value);
    =} STP(10 msec) {=
        lf_print_warning("PrintCount: Safe to process violation!");
        lf_print("Intended time: %lld", in->intended_tag.time - lf_time_start());
    =}
    reaction(t) -> out {=
        lf_set(out, self->c++);
    =}
}

federated reactor {
    c = new Count();
    pr = new Print();
    p = new PrintCount();
    c.out -> p.in;
    p.out -> pr.in;
}
