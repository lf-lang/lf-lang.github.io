---
title: "A First Reactor"
layout: docs
permalink: /docs/handbook/a-first-reactor
oneline: "Writing your first Lingua Franca reactor."
preamble: >
---

$page-showing-target$
<span class="lf-c">
See [setup for C](setup-for-c).
</span>
<span class="lf-cpp">
See [setup for C++](setup-for-cpp).
</span>
<span class="lf-py">
See [setup for Python](setup-for-python).
</span>
<span class="lf-ts">
See [setup for TypeScript](setup-for-typescript).
</span>
<span class="lf-rs">
See [setup for Rust](setup-for-rust).
</span>

## Minimal Example

A minimal but complete Lingua Franca file with one reactor is this:

$start(HelloWorld)$

```lf-c
target C;
main reactor {
    reaction(startup) {=
        printf("Hello World.\n");
    =}
}
```

```lf-cpp
target Cpp;
main reactor {
    reaction(startup) {=
        cout << "Hello World.\n";
    =}
}
```

```lf-py
target Python;
main reactor {
    reaction(startup) {=
        print("Hello World.")
    =}
}
```

```lf-ts
target TypeScript;
main reactor {
    reaction(startup) {=
        console.log("Hello World.");
    =}
}
```

```lf-rs
target Rust;
main reactor {
    reaction(startup) {=
        println!("Hello World.");
    =}
}
```

$end(HelloWorld)$

Every Lingua Franca program begins with a [target specification](target-specification) that specifies the language in which reactions are written. This is also the language of the program(s) generated by the Lingua Franca code generator.

Every LF program also has a $main$ [ or $federated$]{federated} reactor, which is the top level of a hierarchy of contained and interconnected reactors. The above simple example has no contained reactors. Its $main$ reactor has a single reaction to the $startup$ trigger, which causes the reaction to execute at the logical start time of the program. The body of the reaction, delimitted by `{= ... =}`, is ordinary code in the target language.

## Structure of an LF Project

The Lingua Franca tools assume that every such file is put somewhere within a directory called `src`. Create such a directory within your chosen **project root** directory. Put the above code in a file `src/Minimal.lf`. You can compile the code on the [command line](command-line-tools), within [Visual Studio Code](code-plugin) or within the [Epoch IDE](epoch-ide). On the command line this will look like this:

```
    > lfc src/Minimal.lf
    ... output from the code generator and compiler ...
```

<p class="lf-c lf-cpp lf-rs">
After this completes, the `bin` directory will have an executable file called `Minimal`. Executing that file will result, not surprisingly, in printing "Hello World". The generated source files will be within the directory `src-gen`.
</p>
<p class="lf-ts lf-py">
Notice that the generated code will be in subdirectory within `src-gen`.
The output from the code generator will include instructions for executing the generated code:

```lf-ts
#####################################
To run the generated program, use:

    node ...path-to-project.../src-gen/Minimal/Minimal.js

#####################################
```

```lf-py
#####################################
To run the generated program, use:

    python3 ...path-to-project.../src-gen/Minimal/Minimal.py

#####################################
```

</p>

## Reactor Block

A **reactor** is a software component that reacts to input events, timer events, and internal events. It has private state variables that are not visible to any other reactor. Its reactions can consist of altering its own state, sending messages to other reactors, or affecting the environment through some kind of actuation or side effect.

The general structure of a reactor block is as follows:

<div class="lf-c">

```lf
[main or federated] reactor <class-name> ([<parameters>]) {
    input <name>:<type>
    output <name>:<type>
    state <name>:<type>(<value>)
    timer <name>([<offset>, [<period>]])
    logical action <name>[:<type>]
    physical action <name>[:<type>]
    reaction(<triggers>) [<uses>] [=> <effects>] {= ... body ...=}
    <instance-name> = new <class-name>([<parameter-assignments>])
    <instance-name> [, ...] => <instance-name> [, ...] [after <delay>]
}
```

</div>

<div class="lf-cpp lf-rs">

```lf
[main] reactor <class-name> ([<parameters>]) {
    input <name>:<type>
    output <name>:<type>
    state <name>:<type>(<value>)
    timer <name>([<offset>, [<period>]])
    logical action <name>[:<type>]
    physical action <name>[:<type>]
    [const] method <name>(<parameters>):<type> {= ... body ...=}
    reaction(<triggers>) [<uses>] [=> <effects>] {= ... body ...=}
    <instance-name> = new <class-name>([<parameter-assignments>])
    <instance-name> [, ...] => <instance-name> [, ...] [after <delay>]
}
```

</div>

<div class="lf-py lf-ts">

```lf
[main or federated] reactor <class-name> ([<parameters>]) {
    input <name>
    output <name>
    state <name>(<value>)
    timer <name>([<offset>, [<period>]])
    logical action <name>
    physical action <name>
    [const] method <name>(<parameters>) {= ... body ...=}
    reaction(<triggers>) [<uses>] [=> <effects>] {= ... body ...=}
    <instance-name> = new <class-name>([<parameter-assignments>])
    <instance-name> [, ...] => <instance-name> [, ...] [after <delay>]
}
```

</div>

Contents within square brackets are optional, contents within `<...>` are user-defined, and each line may appear zero or more times, as explained in the next pages. Parameter, inputs, outputs, timers, actions, and contained reactors all have names, and the names are required to be distinct from one another.

If the $reactor$ keyword is preceded by $main$[ or $federated$]{federated}, then this reactor will be instantiated and run by the generated code.

Any number of reactors may be defined in one file, and a $main$[ or $federated$]{federated} reactor need not be given a name, but if it is given a name, then that name must match the file name.

Reactors may extend other reactors, inheriting their properties, and a file may import reactors from other files. If an imported LF file contains a $main$[ or $federated$]{federated} reactor, that reactor is ignored (it will not be imported). This makes it easy to create a library of reusable reactors that each come with a test case or demonstration in the form of a main reactor.
